plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

group = 'cn.authok'
version = '1.35.2'
logger.lifecycle("Using version ${version} for ${name} group $group")

task sourceJar(type: Jar, dependsOn: classes) {
  classifier 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

publishing {
  repositories {
    maven {
      // url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      // Note: New non-existing packages uploaded since Feb 2021 use a different URL. See https://central.sonatype.org/pages/ossrh-guide.html
      url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        // Attempts to find the Sonatype credentials.
        // Fallback values are required so the plugin can still be applied.
        username = project.findProperty('ossrhUsername') ?: "Missing ossrhUsername"
        password = project.findProperty('ossrhPassword') ?: "Missing ossrhPassword"
      }
    }
  }
}

afterEvaluate { project ->
  publishing.publications {
    mavenJava(MavenPublication) {
      from components.java

      groupId = project.group
      artifactId = project.name
      version = project.version

      artifact sourceJar
      artifact javadocJar

      pom {
        name = project.name
        packaging = 'jar'
        description = 'Java client library for the Authok platform.'
        url = "https://github.com/authok/authok-java"

        licenses {
          license {
            name = 'The MIT License (MIT)'
            url = "https://raw.githubusercontent.com/authok/authok/master/LICENSE"
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'authok'
            name = 'Authok'
            email = 'developer@authok.cn'
          }
        }

        scm {
          url = "https://github.com/authok/authok-java"
          connection = "scm:git:https://github.com/authok/authok-java.git"
          developerConnection = "scm:git:https://github.com/authok/authok-java.git"
        }
      }
    }
  }

  signing {
    sign project.publishing.publications.mavenJava
    '''publishing.publications.all { publication ->
      sign publication

      required { !gradle.taskGraph.allTasks.findAll { it.name ==~ /.*Sonatype.*/ }.isEmpty() }
    }'''
  }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

compileJava {
    sourceCompatibility '1.8'
    targetCompatibility '1.8'

    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked" << "-Werror"
}

compileTestJava {
    options.compilerArgs << "-Xlint:deprecation" << "-Werror"
}

test {
    testLogging {
        events "skipped", "failed"
        exceptionFormat "short"
    }
}

ext {
    okhttpVersion = '4.9.0'
    hamcrestVersion = '2.2'
}

dependencies {
    implementation "com.squareup.okhttp3:okhttp:${okhttpVersion}"
    implementation "com.squareup.okhttp3:logging-interceptor:${okhttpVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.12.1"
    implementation "com.auth0:java-jwt:3.14.0"
    implementation "net.jodah:failsafe:2.4.1"

    testImplementation "org.bouncycastle:bcprov-jdk15on:1.68"
    testImplementation "org.mockito:mockito-core:3.7.7"
    testImplementation "com.squareup.okhttp3:mockwebserver:${okhttpVersion}"
    testImplementation "org.hamcrest:hamcrest-core:${hamcrestVersion}"
    testImplementation "org.hamcrest:hamcrest-library:${hamcrestVersion}"
    testImplementation "junit:junit:4.13.1"
}
